#!/bin/tcsh -f
#
# This script is for your convenience.  It's NOT EXACTLY THE SAME as going throught the steps in the grading guidelines.
# It's best that you run through section (C) of the grading guidelines manually so you won't get any grading surprises!
#
# This file should be in the "pa2data" subdirectory of the directory where you have your "pa2" executable.  To run it do:
#     chmod 755 ./pa2data/tmux-pa2-C.txt
#     ./pa2data/tmux-pa2-C.txt
# To switch to use a different pane, please type <Ctrl+b> follwed the up/down/left/right key on your keyboard.

setenv SHELL "/bin/tcsh"
set session_name="pa2"
set running=`tmux list-sessions | grep "${session_name}:"`

if ("x$running" != "x") then
    # it's running
    echo "Session '${session_name}' already exists.  Type 'tmux kill-session -t ${session_name}' to kill it."
else
    # create 4 panes, one at the bottom, two on the left, and one on the right

    # create a tmux session
    /bin/rm -f pa2data/*.log
    tmux new-session -d -s ${session_name} 'echo "(window1) "; \
            script -c "pa2data/pa2-C-1.csh" pa2-C.script; \
            echo "[ (1) type <Ctrl+d> to close this pane ]"; cat'
    # split the current pane vertically 95/5
    tmux split-window -v -p 5
    # split the top pane horizontally 60/40
    tmux split-window -h -t top -p 40 'more pa2data/pa2-C-notes.txt; cat'
    # split the left pane vertically 30/70
    tmux split-window -v -t 0 -p 30 '\
            echo "(window2) "; \
            source pa2data/pa2-setup.csh; \
            echo "[ (2) type <Ctrl+d> to run "\""wget -O f1.out "\$"url1"\"" ]"; cat; \
            wget -O f1.out $url1; \
            echo "[ (2) type <Ctrl+d> to run "\""ls -l f1.out"\"" and "\""openssl md5 f1.out"\""]"; cat; \
            ls -l f1.out; \
            openssl md5 f1.out; \
            echo "[ (2) type <Ctrl+d> to run "\""kill "\`"cat pa2.pid"\`""\"" ]"; cat; \
            kill `cat pa2.pid`; \
            echo "[ (2) type <Ctrl+d> to run "\""lsof -i 4"\"" ]"; cat; \
            lsof -i 4; \
            echo "[ (2) type <Ctrl+d> to close this pane ]"; cat'

    tmux attach-session -d -t ${session_name}
endif
